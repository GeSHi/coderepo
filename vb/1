'Dieser Quellcode stammt von http://www.activevb.de
'und kann frei verwendet werden. Für eventuelle Schäden
'wird nicht gehaftet.

'Test for multiline comments _
'next line is _not_ a comment_
Const test = 2&

'Um Fehler oder Fragen zu klären, nutzen Sie bitte unser Forum.
'Ansonsten viel Spaß und Erfolg mit diesem Source!

'------------- Anfang Projektdatei Project1.vbp -------------
'--------- Anfang Formular "Form1" alias Form1.frm  ---------
' Steuerelement: Textfeld "Text6"
' Steuerelement: Textfeld "Text5"
' Steuerelement: Textfeld "Text4"
' Steuerelement: Textfeld "Text3"
' Steuerelement: Textfeld "Text2"
' Steuerelement: Textfeld "Text1"
' Steuerelement: Schaltfläche "Command1"
' Steuerelement: Beschriftungsfeld "Label6"
' Steuerelement: Beschriftungsfeld "Label5"
' Steuerelement: Beschriftungsfeld "Label4"
' Steuerelement: Beschriftungsfeld "Label3"
' Steuerelement: Beschriftungsfeld "Label2"
' Steuerelement: Beschriftungsfeld "Label1"



'Deklaration: Globale Form API-Konstanten
Const HKEY_CLASSES_ROOT = &H80000000
Const REG_SZ = 1&
Const MAX_PATH = 260&

'Deklaration: Globale Form API-Funktionen
Private Declare Function RegCreateKey Lib "advapi32.dll" Alias _
        "RegCreateKeyA" (ByVal hKey As Long, ByVal lpSubKey As _
        String, phkResult As Long) As Long

Private Declare Function RegSetValue Lib "advapi32.dll" Alias _
        "RegSetValueA" (ByVal hKey As Long, ByVal lpSubKey As _
        String, ByVal dwType As Long, ByVal lpData As String, _
        ByVal cbData As Long) As Long
        
Private Declare Function RegCloseKey Lib "advapi32.dll" ( _
        ByVal hKey As Long) As Long

Private Sub Form_Load()
    'Control-Eigenschaften initialisieren
    Label1.Caption = "Anwendungsname"
    Label2.Caption = "Extension"
    Label3.Caption = "Registryname"
    Label4.Caption = "Anwendungspfad"
    Label5.Caption = "Icon - Nummer"
    Label6.Caption = "Icon - Datei"

    Text1.Text = "Selbstgebastelte Text-Editor Datei"
    Text2.Text = Environ("WINDIR") & "\Notepad.exe"
    Text3.Text = "Sg TextEdit Dat"
    Text4.Text = "sgte"
    Text5.Text = "15"
    Text6.Text = Environ("WINDIR") & "\Explorer.exe"

    Command1.Caption = "Registrieren"
End Sub

Private Sub Command1_Click()
    Call Registrieren(Text1.Text, Text2.Text, Text3.Text, Text4.Text, _
    Text6.Text, Val(Text5.Text))
End Sub

Private Sub Registrieren(ByVal strAppName As String, ByVal strAppPath As String, _
    ByVal strAppReg As String, ByVal strAppExt As String, _
    Optional ByVal strIconFile As String = "", Optional ByVal lngIconId As Long)

    'Deklaration: Lokale Prozedur-Variablen
    Dim lngKey As Long
    Dim lngResult As Long

    'Generiert den neuen Eintrag
    lngResult = RegCreateKey(HKEY_CLASSES_ROOT, strAppReg, lngKey)
    lngResult = RegSetValue(lngKey, vbNullString, REG_SZ, strAppName, 0)
    lngResult = RegCloseKey(lngKey)

    'Speichert das Icon, falls vorhanden
    If Len(strIconFile) > 0 Then
        lngResult = RegCreateKey(HKEY_CLASSES_ROOT, strAppReg & "\DefaultIcon", lngKey)

        If Not IsMissing(lngIconId) Then
            strIconFile = RTrim$(strIconFile) & "," & lngIconId
        End If

        lngResult = RegSetValue(lngKey, vbNullString, REG_SZ, strIconFile, 0)
        lngResult = RegCloseKey(lngKey)
    End If

    'Generiert die Assoziotion mit der Endung
    lngResult = RegCreateKey(HKEY_CLASSES_ROOT, "." & strAppExt, lngKey)
    lngResult = RegSetValue(lngKey, vbNullString, REG_SZ, strAppReg, 0)
    lngResult = RegCloseKey(lngKey)

    'Setzt den ausführenden Pfad für die Anwendung
    lngResult = RegCreateKey(HKEY_CLASSES_ROOT, strAppReg, lngKey)
    lngResult = RegSetValue(lngKey, "shell\open\command", REG_SZ, strAppPath & " %1", MAX_PATH)
    lngResult = RegCloseKey(lngKey)
End Sub
'---------- Ende Formular "Form1" alias Form1.frm  ----------
'-------------- Ende Projektdatei Project1.vbp --------------

