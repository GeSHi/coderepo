EXPLICIT_DECLARATIONS
; Declare all internal variables:
COUNT AS INTEGER
ID AS TEXT
OKAY AS INTEGER
COMPANY AS TEXT
DURATION AS NUMBER
POLICY_LOOKUP AS TEXT

; Set constant values:
IF FIRST_RECORD THEN COUNT = 1 ENDIF

; Validate input file values:
IF STATUS = "A" THEN NEXT_RECORD ENDIF
IF BEN_TYPE <> 1 THEN NEXT_RECORD ENDIF

; Validate company codes:
ID = SUBSTR(INPUT_FILE_NAME,LEN(INPUT_FILE_NAME)-2,3)
OKAY = 0
SWITCH(ID)
CASE "ABC": ; Dummy company 1
    COMPANY = "FAKENAME"
    IF CODE="P001" OR CODE="P011" THEN OKAY = 1 ENDIF
CASE "DEF": ; Dummy company 2
    COMPANY = "PSEUDONYM"
    IF CODE="P002" OR CODE="P012" OR CODE="EXTP" THEN OKAY = 1 ENDIF
CASE "GHI": ; Dummy company 3
    COMPANY = "MADEUP"
    IF CODE="P003" OR CODE="P013" THEN OKAY = 1 ENDIF
CASE "JKL": ; Dummy company 4
    COMPANY = "NONSENSE"
    IF CODE="P004" OR CODE="PC04" OR CODE="PH04" OR CODE="PT04" 
    OR CODE="P014" OR (CODE="PR04" AND FREQUENCY<>6) THEN OKAY = 1 ENDIF
DEFAULT:
ENDSWITCH
IF OKAY = 0 THEN NEXT_RECORD ENDIF

; Derive the duration in force:
IF ID <> "JKL" THEN
    DURATION = MAX(POL_YEAR - EFF_YEAR,0)
ELSE ; ID = "JKL"
    DURATION = MAX(MAX(BEN_YEAR,POL_YEAR) - EFF_YEAR,0)
ENDIF
IF DURATION >=75 THEN DURATION = 0 ENDIF

; Output the data:
POLICY_LOOKUP = CODE + "-" + STRVAL(NUM_POL) + "-" + STRVAL(SALES_CODE) + STRVAL(BRANCH_CODE)
GROUP("OUTPUT_FORMAT_1", "PRODUCT_" + ID + ".FAC", "DURATION FOR PRODUCT")
IF ID <> "GHI" THEN
    GROUP("OUTPUT_" + ID, "CROSS_PRODUCT_" + ID + ".FAC", "DURATION FOR CROSS PRODUCT " + COMPANY)
ENDIF
IF (ID = "JKL") AND (CODE <> "PR04") THEN
    GROUP("OUTPUT_FORMAT_1", "PRODUCT_ExclRP04_JKL.FAC", "DURATION FOR PRODUCT")
    GROUP("OUTPUT_JKL", "CROSS_PRODUCT_ExclRP04_JKL.FAC", "DURATION FOR CROSS PRODUCT NONSENSE")
ENDIF

IF DURATION>1 AND (POL_YEAR>1000 OR (ID="JKL" AND BEN_YEAR>1000 AND (CODE="R014" OR CODE="RP04"))) THEN
    SUMMARISE("SUMMARY", "AVERAGE_PRODUCT_" + ID + ".FAC", "AVERAGES BY PRODUCT")
ENDIF
